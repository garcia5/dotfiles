#!/bin/zsh
join-lines() {
  local item
  while read item; do
    echo -n "${(q)item} "
  done
}

# Git
cg () {
    is-git-repo || echo 'Not in git repo'
    cd $(git rev-parse --show-toplevel)
}

is-git-repo() {
    git rev-parse HEAD > /dev/null 2>&1
}

gpush() {
    is-git-repo || echo 'Not in git repo'

    cur_branch=$(git branch --show-current)
    has_remote=$(git branch -r --list "origin/$cur_branch")
    if [[ $has_remote ]]; then
        git push "$@"
    else
        echo "No remote branch found, creating one"
        git push --set-upstream origin "$cur_branch" "$@"
    fi
}

# CD helpers
function cdp() {
    cd "${HOME}/Projects/${1}"
}
compdef '_path_files -/ -W $HOME/Projects' cdp
function cdw() {
    cd "${HOME}/work/${1}"
}
compdef '_path_files -/ -W $HOME/work' cdw

# send keys to all visible wezterm panes
ws() {
    local cmd="$1"

    if [[ -z "$cmd" ]]; then
        return
    fi

    local wez_list="$(wezterm cli list --format json)"
    local cur_pane_id="$WEZTERM_PANE"

    # get current tab
    local cur_tab_id="$(echo $wez_list | jq -r --arg PANE $WEZTERM_PANE '.[] | select(.pane_id==($PANE | tonumber)).tab_id')"
    # get all panes in current tab
    local cur_tab_panes="$(echo $wez_list | jq -r --arg TAB $cur_tab_id '.[] | select(.tab_id==($TAB | tonumber)).pane_id')"

    for pane_id in $(echo $cur_tab_panes); do
        echo "${cmd}" | wezterm cli send-text --no-paste --pane-id "${pane_id}"
    done
}

# update all panes in current tab to the given directory
cda() {
    local target_dir="$1"
    if [[ -z "${target_dir}" ]]; then
        target_dir="$HOME"
    fi

    ws "cd ${target_dir}"
}
compdef '_path_files -/' cda

# FZF
zle -N ctrl-t-widget
ctrl-t-widget() {
    local result=''
    # If the current command is 'git add ', 'git restore ', or 'git checkout ' fuzzy find changed files
    if [[ $LBUFFER =~ '^git (add|restore|checkout) ' ]]; then
        result=$(fzf-git-changed | join-lines)
    # Find only '.spec' files
    elif [[ $LBUFFER =~ '^(yarn|pnpm) test ' ]]; then
        result=$(fzf-js-test | join-lines)
    elif [[ $LBUFFER =~ 'pytest' ]]; then
        result=$(fzf-pytest | join-lines)
    # Otherwise do default and fuzzy search all files
    else
        result=$(eval $FZF_CTRL_T_COMMAND | eval "fzf $FZF_CTRL_T_OPTS")
    fi
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' ctrl-t-widget

fzf-js-test() {
    local cmd="rg -n -m 1 'describe' --glob '*.spec.ts' --line-number --no-heading"
    eval "$cmd" |
    fzf \
        --multi \
        --height 100% \
        --delimiter : \
        --nth 1 \
        --header 'ctrl-f: All files / ctrl-g Test files' \
        --bind "ctrl-f:change-prompt(Files> )+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-prompt(Test Files> )+reload($cmd)" \
        --prompt "Test Files> " \
        --preview "bat --color=always --line-range=:40 --style=plain --highlight-line={2} {1}" |
    cut -d: -f1
}

fzf-pytest() {
    local cmd="fd -tf -g 'test_*.py'"
    eval "$cmd" |
    fzf \
        --multi \
        --header 'ctrl-f: All files / ctrl-g Test files' \
        --bind "ctrl-f:change-prompt(Files> )+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-prompt(Test Files> )+reload($cmd)" \
        --prompt "Test Files> " \
        --preview "bat --color=always {}"
    cut -d: -f1
}

fzf-git-checkout() {
    is-git-repo || echo 'Not in git repo'

    # pass args straight to git-checkout if provided
    arg=$1
    if [[ -n $arg ]]; then
        git switch "$@"
        return
    fi

    local git_cmd="git branch --color=always --sort=-committerdate"
    local format="grep -v HEAD | sed 's/^..//' | sed 's,remotes/,,' | cut -d' ' -f1"
    eval "$git_cmd --all | $format" |
    fzf \
        --ansi \
        --header "ctrl-a: checkout -b / ctrl-t: checkout --track / ctrl-r: All branches / ctrl-l: Local branches" \
        --bind "enter:execute(git switch {..})+accept" \
        --bind "ctrl-a:execute(git switch -c {q})+accept" \
        --bind "ctrl-t:execute(git switch --track {..})+accept" \
        --bind "ctrl-r:reload($git_cmd --all | $format)+change-prompt(Branches> )" \
        --bind "ctrl-l:reload($git_cmd | $format)+change-prompt(Branches (local)> )" \
        --prompt "Braches> " \
        --preview-window 'right:50%' \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}'
}

zle -N git-commit-widget
fzf-git-commits() {
    is-git-repo || echo 'Not in git repo'

    cmd="git log --date-order --abbrev-commit --oneline --decorate --color=always"
    eval "$cmd" |
    fzf \
        --ansi \
        --multi \
        --height '70%' \
        --prompt "Commits> " \
        --preview-window 'down,70%' \
        --preview 'git show --color=always --format=short --stat -p {1} | fzf-delta' |
    cut -d' ' -f1
}
git-commit-widget() {
    result=$(fzf-git-commits | join-lines)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' git-commit-widget

zle -N git-branch-widget
fzf-git-branch() {
    is-git-repo || echo 'Not in git repo'
    local git_cmd="git branch --color=always --sort=-committerdate"
    local format="grep -v HEAD | sed 's/^..//' | sed 's,remotes/,,' | cut -d' ' -f1"

    eval "$git_cmd --all | $format" |
    fzf \
        --ansi \
        --multi \
        --prompt "Branches> " \
        --header "ctrl-r: All branches / ctrl-l: Local branches" \
        --preview-window 'right,50%' \
        --bind "ctrl-r:reload($git_cmd --all | $format)+change-prompt(Branches> )" \
        --bind "ctrl-l:reload($git_cmd | $format)+change-prompt(Branches (local)> )" \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}'
}
git-branch-widget() {
    result=$(fzf-git-branch | join-lines)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' git-branch-widget

fzf-git-changed() {
    is-git-repo || echo 'Not in git repo'
    cmd="git -c color.status=always status --short"

    eval "$cmd" |
    fzf \
        --multi \
        --height '70%' \
        --ansi \
        --header 'ctrl-f: All files / ctrl-g: Changed files' \
        --bind "ctrl-f:change-prompt(Files> )+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-prompt(Changed Files> )+reload($cmd)" \
        --prompt "Changed Files> " \
        --preview-window 'right,70%' \
        --preview 'delta-git-diff {..}' |
    sed 's/.* //'
}

zle -N fzf-ls-widget
fzf-ls() {
    file_preview="bat --color=always --line-range=:40 --style=numbers,changes {}"
    dir_preview="eza --color=always -T --level 2 {}"

    fd -tf |
    fzf \
        --ansi \
        --multi \
        --header "ctrl-d: Directories / ctrl-f: Files" \
        --bind "ctrl-d:change-prompt(Directories> )+reload(fd -td)+change-preview(${dir_preview})" \
        --bind "ctrl-f:change-prompt(Files> )+reload(fd -tf)+change-preview(${file_preview})" \
        --preview "${file_preview}"\
        --prompt "Files> "
}
fzf-ls-widget() {
    result=$(fzf-ls | join-lines)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' fzf-ls-widget

fzf-cd() {
    fd -td -H -E '*.git' |
    fzf \
        --ansi \
        --no-multi \
        --preview='eza -hal {..}' \
        --prompt "Directories> "
}

# Others
source-envfile() {
    set -a
    source "$1"
    set +a
}
