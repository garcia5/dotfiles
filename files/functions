#!/bin/zsh
export RED='\033[0;31m'
export GREEN='\033[0;32m'
export YELLOW='\033[0;33m'
export BLUE='\033[0;34m'
export NC='\033[0m' # No Color

# Git
cg () {
    is-git-repo || { echo 'Not in git repo'; return 0 }
    cd $(git rev-parse --show-toplevel)
}

is-git-repo() {
    git rev-parse HEAD > /dev/null 2>&1
}

# CD helpers
function cdp() {
    cd "${HOME}/Projects/${1}"
}
compdef '_path_files -/ -W $HOME/Projects' cdp
function cdw() {
    cd "${HOME}/work/${1}"
}
compdef '_path_files -/ -W $HOME/work' cdw

# send keys to all visible wezterm panes
ws() {
    local cmd="$@"

    if [[ -z "$cmd" ]]; then
        return
    fi

    local wez_list="$(wezterm cli list --format json)"
    local cur_pane_id="$WEZTERM_PANE"

    # get current tab
    local cur_tab_id="$(echo $wez_list | jq -r --arg PANE $WEZTERM_PANE '.[] | select(.pane_id==($PANE | tonumber)).tab_id')"
    # get all panes in current tab
    local cur_tab_panes="$(echo $wez_list | jq -r --arg TAB $cur_tab_id '.[] | select(.tab_id==($TAB | tonumber)).pane_id')"

    for pane_id in $(echo $cur_tab_panes); do
        echo "${cmd}" | wezterm cli send-text --no-paste --pane-id "${pane_id}"
    done
}

# update all panes in current tab/window to the given directory
cda() {
    local target_dir="$1"
    if [[ -z "${target_dir}" ]]; then
        target_dir="$HOME"
    fi

    if [ -n "${WEZTERM_PANE}" ]; then
        ws "cd ${target_dir}"
    elif [ -n "${TMUX}" ]; then
        tmux list-panes -F '#{pane_id} #{pane_current_command}' | grep -E 'zsh|tmux' | cut -d' ' -f1 | xargs -I {} tmux send-keys -t {} "cd ${target_dir}" Enter
    fi
}
compdef '_path_files -/' cda

# FZF
zle -N ctrl-t-widget
ctrl-t-widget() {
    local result=''
    # If the current command is 'git add ', 'git restore ', or 'git checkout ' fuzzy find changed files
    if [[ $LBUFFER =~ '^git (add|restore|checkout|stash) ' || $LBUFFER =~ '^ga ' ]]; then
        result=$(fzf-git-changed)
    # Find only '.spec' files
    elif [[ $LBUFFER =~ '^(yarn|pnpm) test ' ]]; then
        result=$(fzf-js-test)
    # Find only test_*.py files
    elif [[ $LBUFFER =~ 'pytest' ]]; then
        result=$(fzf-pytest)
    # Find only python files, replace '/' with '.' to properly run locally as module
    elif [[ $LBUFFER =~ 'python \-m ' ]]; then
        result=$(fzf-python-module)
    # Otherwise do default and fuzzy search all files
    else
        result=$(fzf-ctrl-t)
    fi
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' ctrl-t-widget

vs() {
    eval "$FZF_CTRL_T_COMMAND" | fzf \
        --scheme path \
        --ghost 'Search Files' \
        --tmux 'center,60%' \
        --preview 'bat --color=always --style=numbers,changes {}' \
        --header 'enter: open in \$EDITOR' \
        --bind 'enter:become(${EDITOR:-nvim} {+})'
}

fzf-ctrl-t() {
    eval "$FZF_CTRL_T_COMMAND" | fzf \
        --scheme path \
        --print0 \
        --ghost 'Search Files' \
        --preview 'bat --color=always --style=numbers,changes {}' |
    xargs -0 echo
}

fzf-js-test() {
    local cmd="rg -n -m 1 'describe' --glob '*.spec.ts' --line-number --no-heading"
    local fzf_cmd="fzf \
        --multi \
        --print0 \
        --scheme path \
        --delimiter : \
        --nth 1 \
        --accept-nth 1 \
        --ghost='Search Test Files' \
        --header 'ctrl-f: All files | ctrl-g Test files' \
        --bind 'ctrl-f:change-ghost(Search All Files)+reload($FZF_CTRL_T_COMMAND)' \
        --bind 'ctrl-g:change-ghost(Search Test Files)+reload($cmd)' \
        --preview 'bat --color=always --style=plain --highlight-line={2} {1}'"
    eval "$cmd" | "$fzf_cmd" | xargs -0 echo
}

fzf-pytest() {
    local cmd="$FZF_CTRL_T_COMMAND -g 'test_*.py'"
    eval "$cmd" |
    fzf \
        --multi \
        --print0 \
        --scheme path \
        --header 'ctrl-f: All files | ctrl-g Test files' \
        --bind "ctrl-f:change-ghost(Search All Files)+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-ghost(Search Test Files)+reload($cmd)" \
        --ghost "Search Test Files" \
        --preview "bat --color=always {}" | xargs -0 echo
}

fzf-python-module() {
    local cmd="$FZF_CTRL_T_COMMAND -e '.py'"

    eval "$cmd" |
    fzf \
        --no-multi \
        --print0 \
        --scheme path \
        --header 'ctrl-f: All files | ctrl-g Python files' \
        --bind "ctrl-f:change-ghost(Search All Files)+reload($FZF_CTRL_T_COMMAND)" \
        --bind "ctrl-g:change-ghost(Search Python Files)+reload($cmd)" \
        --ghost "Search Python Files" \
        --preview "bat --color=always {}" |
    sed 's$/$.$g' |
    sed 's/.py$//' |
    xargs -0 echo
}

fzf-git-checkout() {
    is-git-repo || { echo 'Not in git repo'; return 0 }

    # pass args straight to git-checkout if provided
    arg=$1
    if [[ -n $arg ]]; then
        git switch "$@"
        return
    fi

    local git_cmd="git branch --color=always --sort=-committerdate"
    local format="grep -v HEAD | sed 's/^..//' | sed 's,remotes/,,' | cut -d' ' -f1"
    eval "$git_cmd --all | $format" |
    fzf \
        --ansi \
        --header "ctrl-a: switch -c | ctrl-t: switch --track | ctrl-r: All branches | ctrl-l: Local branches" \
        --bind "enter:execute(git switch {..})+accept" \
        --bind "ctrl-a:execute(git switch -c {q})+accept" \
        --bind "ctrl-t:execute(git switch --track {..})+accept" \
        --bind "ctrl-r:reload($git_cmd --all | $format)+change-ghost(Search All Branches)" \
        --bind "ctrl-l:reload($git_cmd | $format)+change-ghost(Search Local Branches)" \
        --ghost "All Branches" \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}'
}

zle -N git-commit-widget
fzf-git-commits() {
    is-git-repo || { echo 'Not in git repo'; return 0 }

    diff_cmd="git show --color=always --format=medium --stat -p {1} | fzf-delta"
    diffview_cmd='nvim -c "DiffviewOpen {1}^..{1}"'
    # use "--raw" mode from 0.66.0 to keep nearby commits visible even after filtering
    # alt-r to toggle raw mode
    local fzf_cmd="fzf \
        --ansi \
        --multi \
        --print0 \
        --raw \
        --bind 'enter:transform:[[ \${FZF_RAW-1} = 1 ]] && echo accept || echo bell' \
        --bind 'result:best' \
        --bind 'alt-r:toggle-raw' \
        --scheme 'history' \
        --ghost 'Search Commits' \
        --accept-nth 1 \
        --header 'Ctrl-a: open in delta | Ctrl-t: open in nvim' \
        --bind 'ctrl-a:execute($diff_cmd)' \
        --bind 'ctrl-t:execute($diffview_cmd)+abort' \
        --preview '$diff_cmd'"

    if [[ "$1" == "-q" ]]; then
        fzf_cmd="$fzf_cmd --sync --bind 'start:jump-accept'"
    fi

    cmd="git log --date-order --abbrev-commit --oneline --decorate --color=always"
    eval "$cmd" | eval "$fzf_cmd" | xargs -0 echo
}

git-commit-widget() {
    result=$(fzf-git-commits)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' git-commit-widget

zle -N git-branch-widget
fzf-git-branch() {
    is-git-repo || { echo 'Not in git repo'; return 0 }
    local prompt="${1:-Branches}"
    local git_cmd="git branch --color=always --sort=-committerdate"
    local format="grep -v HEAD | sed 's/^..//' | sed 's,remotes/,,' | cut -d' ' -f1"

    eval "$git_cmd | $format" |
    fzf \
        --ansi \
        --multi \
        --print0 \
        --ghost "${prompt}" \
        --header "ctrl-a: All branches | ctrl-l: Local branches" \
        --bind "ctrl-a:reload($git_cmd --all | $format)+change-ghost(Search All Branches)" \
        --bind "ctrl-l:reload($git_cmd | $format)+change-ghost(Search Local Branches)" \
        --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" {}' |
        xargs -0 echo
}
git-branch-widget() {
    result=$(fzf-git-branch)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' git-branch-widget

fzf-git-changed() {
    cmd="git -c color.status=always status --short"
    diff_cmd="delta-git-diff {}"
    bat_cmd="bat --color=always {}"

    eval "$cmd" |
    fzf \
        --multi \
        --ansi \
        --print0 \
        --scheme path \
        --accept-nth 2 \
        --nth 2 \
        --header 'ctrl-f: All files | ctrl-g: Changed files' \
        --bind "ctrl-f:change-ghost(Search Files)+change-preview($bat_cmd)+reload($FZF_DEFAULT_COMMAND)" \
        --bind "ctrl-g:change-ghost(Search Changed Files)+change-preview($diff_cmd)+reload($cmd)" \
        --ghost "Search Changed Files" \
        --preview 'delta-git-diff {..}' | xargs -0 echo
}

zle -N fzf-ls-widget
fzf-ls() {
    file_preview="bat --color=always --line-range=:40 --style=numbers,changes {}"
    dir_preview="eza --color=always -T --level 2 {}"

    file_cmd="$FZF_CTRL_T_COMMAND"
    dir_cmd="fd -td"

    eval "$file_cmd" |
    fzf \
        --ansi \
        --multi \
        --print0 \
        --scheme path \
        --header "ctrl-d: Directories / ctrl-f: Files" \
        --bind "ctrl-d:change-ghost(Search Directories)+reload(${dir_cmd})+change-preview(${dir_preview})" \
        --bind "ctrl-f:change-ghost(Search Files)+reload(${file_cmd})+change-preview(${file_preview})" \
        --preview "${file_preview}" \
        --list-label "Search Files" | xargs -0 echo
}
fzf-ls-widget() {
    result=$(fzf-ls)
    zle reset-prompt
    LBUFFER+=$result
}
bindkey '' fzf-ls-widget

fzf-cd() {
    fd -td -H -E '*.git' |
    fzf \
        --no-multi \
        --scheme "path" \
        --preview='eza -hal {..}' \
        --ghost "Search Directories"
}
c() {
    local out="$(fzf-cd)"
    if [ -z "$out" ]; then
        return
    fi

    cd "$out"
}

# Others
source-envfile() {
    set -a
    source "$1"
    set +a
}
